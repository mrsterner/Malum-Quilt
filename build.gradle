plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
}

archivesBaseName = project.archives_base_name
version = "${project.version}+${libs.versions.minecraft.get()}"
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/malum.accesswidener")
}


repositories {
	maven {
		url 'https://api.modrinth.com/maven'
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven { url 'https://maven.quiltmc.org/repository/snapshot' }
	maven { url 'https://maven.terraformersmc.com' }
	maven { url 'https://ladysnake.jfrog.io/artifactory/mods' }
	maven { url 'https://jitpack.io' }
	maven { url "https://maven.shedaniel.me" }
	maven {
		name "reach entity attributes"
		url "https://maven.jamieswhiteshirt.com/libs-release"
	}
	mavenCentral()
}

dependencies {
	minecraft libs.minecraft
	mappings variantOf(libs.quilt.mappings) { classifier 'intermediary-v2' }

	modImplementation libs.quilt.loader


	modImplementation libs.quilted.fabric.api

	modRuntimeOnly("maven.modrinth:sodium:mc1.19.3-0.4.6")

	modImplementation(libs.trinkets) {
		exclude group: "net.fabricmc"
		exclude group: "net.fabricmc.fabric-api"
		exclude group: "com.terraformersmc"
	}

	modImplementation(libs.cardinal.components.entity) {
		exclude group: "net.fabricmc"
		exclude group: "net.fabricmc.fabric-api"
	}
	modImplementation(libs.cardinal.components.base) {
		exclude group: "net.fabricmc"
		exclude group: "net.fabricmc.fabric-api"
	}

	modImplementation(libs.fabric.asm)
	include(libs.fabric.asm)
	include(libs.satin)
	modImplementation include("maven.modrinth:midnightlib:$midnightlib_version")
	modImplementation include("com.github.mrsterner:LodestoneLib-Quilt:$lodestone_version")
	modImplementation("com.jamieswhiteshirt:reach-entity-attributes:2.3.1")
	implementation(libs.mixin.extras)
	include(libs.mixin.extras)
	annotationProcessor(libs.mixin.extras)
}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Still required by IDEs such as Eclipse and Visual Studio Code
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	// If this mod is going to be a library, then it should also generate Javadocs in order to aid with development.
	// Uncomment this line to generate them.
	// withJavadocJar()
}

// If you plan to use a different file for the license, don't forget to change the file name here!
jar {
	from('LICENSE') {
		rename { "${it}_${archivesBaseName}" }
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
